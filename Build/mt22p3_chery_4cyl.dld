
MEMORY
{
   /* FLASH Allocation */
   pml_mfg:         org = 0x00000000, len = 0x00004000   /* 16K  */
   user_boot:       org = 0x00004000, len = 0x0000C000   /* 48K  */
   reserved:        org = 0x00010000, len = 0x00008000   /* 32K  */
   eeprom:          org = 0x00018000, len = 0x00008000   /* 32K  */
   cal:             org = 0x00020000, len = 0x00020000   /* 128K */
   app_code:        org = 0x00040000, len = 0x00137000   /* 1244K */
   flashcode:       org = 0x00177000, len = 0x00005400   /* 21K */
   tsw_code:        org = 0x0017C400, len = 0x00003C00   /* 15K */

   /* RAM Allocation */
   nvram:            org = 0x40000000, len = 0x00002000  /*  8K  */
   unclear_ram:      org = 0x40002000, len = 0x00000010  /*  16 bytes  */
   app_ram:          org = 0x40002010, len = 0x0000CFF0  /*  52K - 16 bytes */
   stack:            org = 0x4000f000, len = 0x00001000  /*  4K  */
}

SECTIONS
{

  .calibration :
  {
      start_of_calrom = .;
      STORE(0xAAAA,2)
      hwiocald.o (.text_vle)
      vsepcald.o (.text_vle)
      vehicle_can_cald.o (.text_vle)
      esc_cal.o(.text_vle)
      emsdcald.o(.text_vle)
      eosdcald.o(.text_vle)
      epsdcald.o(.text_vle)
      injdcald.o(.text_vle)
      prgdcald.o(.text_vle)
      vvtdcald.o(.text_vle)
      CalVariable.o (.text_vle)
      end_of_calrom = .;
  } > cal

  .flashcode:
  {
      start_of_flashcode = .;
      dd_swt.o(.text_vle)
      CFlashDepletionRecover.o (.text_vle) 
      cblankcheck.o  (.text_vle)
      cchecksum.o  (.text_vle)
      cfactorymarginreadcheck.o  (.text_vle)
      cflasharrayintegritycheck.o  (.text_vle)
      cflashecclogiccheck.o  (.text_vle)
      cflasherase.o  (.text_vle)
      cflashinit.o (.text_vle) 
      cflashprogram.o  (.text_vle)
      cflashresume.o  (.text_vle)
      cflashsuspend.o  (.text_vle)
      cgetlock.o  (.text_vle)
      cprogramverify.o  (.text_vle)
      csetlock.o  (.text_vle)
      dd_c90fl_flash.o  (.text_vle)
      dd_c90fl_flash_init.o (.text_vle) 
      dd_c90lc_flash.o (.text_vle) 
      dd_c90lc_flash_init.o (.text_vle) 
      dd_flash_memory_interface.o (.text_vle) 
      dd_flash_mpc5634.o (.text_vle) 
      dd_flash_mpc5644.o  (.text_vle)
      ssd_c90fl_FlashDepletionRecover.o (.text_vle) 
      ssd_c90fl_blankcheck.o (.text_vle) 
      ssd_c90fl_checksum.o (.text_vle) 
      ssd_c90fl_factorymarginreadcheck.o (.text_vle) 
      ssd_c90fl_flasharrayintegritycheck.o (.text_vle) 
      ssd_c90fl_flashecclogiccheck.o  (.text_vle)
      ssd_c90fl_flasherase.o (.text_vle) 
      ssd_c90fl_flashinit.o (.text_vle) 
      ssd_c90fl_flashprogram.o (.text_vle)
      ssd_c90fl_flashresume.o (.text_vle) 
      ssd_c90fl_flashsuspend.o (.text_vle) 
      ssd_c90fl_getlock.o (.text_vle) 
      ssd_c90fl_programverify.o (.text_vle) 
      ssd_c90fl_setlock.o (.text_vle) 
      ssd_c90lc_BlankCheck.o (.text_vle) 
      ssd_c90lc_CheckSum.o (.text_vle) 
      ssd_c90lc_FactoryMarginReadCheck.o (.text_vle) 
      ssd_c90lc_FlashArrayIntegrityCheck.o (.text_vle) 
      ssd_c90lc_FlashECCLogicCheck.o (.text_vle) 
      ssd_c90lc_FlashErase.o (.text_vle) 
      ssd_c90lc_FlashInit.o (.text_vle) 
      ssd_c90lc_FlashProgram.o (.text_vle) 
      ssd_c90lc_FlashResume.o (.text_vle) 
      ssd_c90lc_FlashSuspend.o (.text_vle) 
      ssd_c90lc_GetLock.o (.text_vle) 
      ssd_c90lc_ProgramVerify.o (.text_vle)
      ssd_c90lc_SetLock.o (.text_vle) 
      hal_eeprom_mfg.o (.text_vle)
      hal_eeprom_nvm.o (.text_vle)
      hal_emulated_eeprom.o (.text_vle)
      inst_config.o (.text_vle)
      inst_overlay.o (.text_vle)
      inst_overlay_mem.o (.text_vle)
      inst_overlay_nvm.o (.text_vle)

      end_of_flashcode = .;
  } > flashcode
  
    .testability:
    {
        start_of_testability = .;
        end_of_testability = .;
  } > tsw_code
  
  .ucram:
  {
    hal_ucram.o (.bss)
  } > unclear_ram

  GROUP :
  {
     start_of_vector_table = .;
     .vector_table:
     {
        EXCEPTION_TABLE_ADDRESS = .;
        dd_intc_Exception_Not_Available.o (.text_vle)
        dd_intc_Exception_Machine_Check.o (.text_vle)
        dd_intc_Exception_Data_Storage.o (.text_vle)
        dd_intc_Exception_Instruction_Storage.o (.text_vle)
        dd_intc_expection_external.o (.text_vle)
        dd_intc_Exception_Alignment.o (.text_vle)
        dd_intc_Exception_Program.o (.text_vle)
        dd_intc_Exception_Fp_Unavailable.o (.text_vle)
        dd_intc_Exception_System_Call.o (.text_vle)
        dd_intc_Exception_Aux_Proc_Unavailable.o (.text_vle)
        dd_intc_Exception_Decrementer.o (.text_vle)
        dd_intc_Exception_Fixed_Interval_Timer.o (.text_vle)
        dd_intc_Exception_Watchdog.o (.text_vle)
        dd_intc_Exception_Data_Tlb.o (.text_vle)
        dd_intc_Exception_Instruction_Tlb.o (.text_vle)
        dd_intc_Exception_Debug.o (.text_vle)
        dd_intc_Exception_Spe_Apu_Unavail.o (.text_vle)
        dd_intc_Exception_Spe_Fp_Data.o (.text_vle)
        dd_intc_Exception_Spe_Fp_Round.o (.text_vle)
        dd_intc_Exception_Unused_Exception.o (.text_vle)
      
     }
     end_of_vector_table = .;
     . = start_of_vector_table + 0x1400; /* 5K for vector table */

     start_of_approm = .;
     .app_sec:
     {
         main.o (.text_vle)
         /* locate main function at 0x00041400 exactly, it's for the jumping from boot block */
         *(.text_vle)         
     }
     end_of_approm = .;
     
     __DATA_ROM = .;
  } > app_code

  GROUP :
  {
     /* It's the start of NVRAM */
     start_of_nvram = .;

     /* offset 0x0C from start of NVRAM - contains check pattern and seq no. */
     . += 0x0C;
     start_of_mfgram = .;
     .mfg :
     {

     }
     end_of_mfgram = .;

     /* offset 264 bytes from start of NVRAM. */
     . = start_of_nvram + 264;
     start_of_crit_nvram = .; 
     .c_nvram :
     {
        

     } 
     end_of_crit_nvram = .;

     /* offset 256 bytes from start of critical NVRAM. */
     . = start_of_crit_nvram + 256;
     start_of_noncrit_nvram = .;
     .nc_nvram :
     {

     }
     end_of_noncrit_nvram = .;

     end_of_nvram = start_of_nvram + SIZEOF( nvram );
  } > nvram

  GROUP :
  {
     /* Load memory at the specified address with the contents of the */
     /* .data sections specified in this file.                        */
      start_of_appram = .;
      __DATA_RAM = . ;

     .data (DATA) LOAD(__DATA_ROM): {} 
     .sdata (DATA) LOAD(ADDR(.sdata) - ADDR(.data) + __DATA_ROM) : { }
      
      __DATA_END	= .;
      
      __BSS_START = . ;
 
      .sbss (BSS) : { }
      .bss (BSS) : { }
       start_of_hlsbss = .;
      .hlsbss (BSS) : { }
      end_of_hlsbss = .;
      __BSS_END = . ;
      end_of_appram = .;

  } > app_ram
}


_SDA_BASE_            = ADDR( app_ram );

HWIO_DATA_ROM_START   = __DATA_ROM;
HWIO_DATA_RAM_START   = __DATA_RAM;
HWIO_DATA_SIZE        = __DATA_END - __DATA_RAM;

__SP_INIT             = ADDR(stack)+SIZEOF(stack); 
__SP_SIZE             = SIZEOF(stack);             
__SP_END              = ADDR(stack);               

start_of_stack        = ADDR(stack);
end_of_stack          = ADDR(stack)+SIZEOF(stack); 

HWIO_NVRAM_START      = ADDR( nvram );
HWIO_NVRAM_SIZE       = SIZEOF( nvram );

HWIO_BSS_START        = __BSS_START;
HWIO_BSS_SIZE         = __BSS_END - __BSS_START;

HWIO_HLSBSS_START        = start_of_hlsbss;
HWIO_HLSBSS_SIZE         = end_of_hlsbss - start_of_hlsbss;

SIZE_OF_ALLOC_APPROM  = SIZEOF( app_code )+SIZEOF( flashcode );
SIZE_OF_ALLOC_CALROM  = SIZEOF( cal );
SIZE_OF_ALLOC_APPRAM  = SIZEOF( app_ram );
SIZE_OF_ALLOC_TSWROM  = SIZEOF( tsw_code );

SIZE_OF_USED_APPROM  = end_of_approm - start_of_vector_table + __DATA_END - __DATA_RAM;
SIZE_OF_USED_CALROM  = end_of_calrom - start_of_calrom;
SIZE_OF_USED_APPRAM  = __BSS_END - __DATA_RAM;
SIZE_OF_USED_TSWROM  = end_of_testability - start_of_testability; 

SIZE_OF_FREE_APPROM  = SIZE_OF_ALLOC_APPROM - SIZE_OF_USED_APPROM;
SIZE_OF_FREE_CALROM  = SIZE_OF_ALLOC_CALROM - SIZE_OF_USED_CALROM;
SIZE_OF_FREE_APPRAM  = SIZE_OF_ALLOC_APPRAM - SIZE_OF_USED_APPRAM;
SIZE_OF_FREE_TSWROM  = SIZE_OF_ALLOC_TSWROM - SIZE_OF_USED_TSWROM;


